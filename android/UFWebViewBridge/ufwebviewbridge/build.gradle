apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

group='org.xmmstudio.ufwebviewbridge'

android {
    compileSdkVersion 25
    buildToolsVersion "24.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        publishNonDefault true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

    }

    android.libraryVariants.all { variant ->
        String suffix = variant.name.capitalize()
        Task bundleTask = tasks.findByName("bundle${suffix}")
        Task mergeAssetsTask = tasks.findByName("merge${suffix}Assets")

        Task copyAssetFileTask = tasks.create(name: "copy${suffix}AssetFile", type: Copy) {
            from "../../.."
            into "${mergeAssetsTask.outputDir}"
            include("*.js", "*.html")
        }

        copyAssetFileTask.dependsOn mergeAssetsTask
        bundleTask.dependsOn copyAssetFileTask
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'

    compile 'com.google.code.gson:gson:2.8.0'
}
